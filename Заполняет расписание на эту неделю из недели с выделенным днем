
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ЗаполнитьРасписаниеНаСервере(ПараметрКоманды);
	ОповеститьОбИзменении(ПараметрКоманды);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасписаниеНаСервере(ВыделенныйДень)
	
	// Получить начало этой недели.
	НачалоЭтойНедели = НачалоНедели(ВыделенныйДень.Дата);
	НачалоБудущейНедели = КонецНедели(ТекущаяДатаСеанса()) + 1;
	СдвигВСекундах = НачалоБудущейНедели - НачалоЭтойНедели;
	
	// Перебрать все дни этой недели
	Для КоличествоДобавляемыхДней = 0 По 4 Цикл
		ДатаСтарого = НачалоЭтойНедели + 60 * 60 * 24 * КоличествоДобавляемыхДней;
		НомерСтарого = Серверный.ПолучитьНомер(ДатаСтарого);
		
		// Проверить, что в базе есть старый документ оригинал
		СтарыйДокумент = Документы.УчебныйДень.НайтиПоНомеру(НомерСтарого);
		
		// Если в базе нет документа-оригинала - перейти к следующему дню недели
		
		Если СтарыйДокумент.Пустая() Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Нет в списке " + Формат(ДатаСтарого,"ДФ='д ММММ гггг. дддд'");
			Сообщение.Сообщить();
			Продолжить;
		КонецЕсли;
		
		// Вычислить дату и номер для нового документа
		ДатаНового = ДатаСтарого + СдвигВСекундах;
		НомерНового = Серверный.ПолучитьНомер(ДатаНового);
		// Проверить что в базе нет нового документа по дате
		Если Документы.УчебныйДень.НайтиПоНомеру(НомерНового).Пустая() Тогда
			// Создать и записать новый документ
			НовыйДокумент = Документы.УчебныйДень.СоздатьДокумент();
			НовыйДокумент.Дата = ДатаНового;
			
			Для Каждого СтрокаСтарого Из СтарыйДокумент.Уроки Цикл
				НоваяСтрока = НовыйДокумент.Уроки.Добавить();
				НоваяСтрока.Предмет = СтрокаСтарого.Предмет;
			КонецЦикла;
			НовыйДокумент.Записать();
			
		Иначе
			// Уже есть новый документ на эту дату перейти к следующему дню
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Документ " + Формат(ДатаНового, "ДФ='д ММММ гггг. дддд'") + " уже в списке!";
			Сообщение.Сообщить();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
